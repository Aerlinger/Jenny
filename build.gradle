// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        pub = [
                publishVersion: '0.1.1',
                userOrg       : 'landerlyoung',
                groupId       : 'io.github.landerlyoung.jenny',
                desc          : 'Java/Android Jni glue code generator',
                website       : 'https://github.com/LanderlYoung/Jenny',
                licences      : ['Apache-2.0']
        ]

        kotlin_version = '1.3.50'

        configRepo = { repo ->
            repo.mavenLocal()
            repo.jcenter()
            repo.google()
        }
    }

    configRepo repositories

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9.1'
        classpath 'com.android.tools.build:gradle:3.5.1'
    }
}

allprojects {
    // basic
    configRepo buildscript.repositories
    configRepo repositories
    version = pub.publishVersion
    description = pub.desc

    // publish
    final def publishProject = [
            "annotation", "compiler"
    ] as Set

    if (publishProject.contains(name)) {
        apply plugin: 'com.novoda.bintray-release'

        publish {
            publishVersion = version
            artifactId = "jenny-$name"
            userOrg = pub.userOrg
            groupId = pub.groupId
            desc = pub.desc
            website = pub.website
            licences = pub.licences

            // account config from commpand line
            // -PbintrayUser=xxx
            // -PbintrayKey=xxx
            // -PdryRun=xxx
        }
    }
}
